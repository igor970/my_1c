
//Анализирует символы строки, оставляет только цифры 0-9
// Параметры:
//  НомерТелефона - Строка - номер телефона.
// Возвращаемое значение:
//   - Строка
//
Функция ОставитьТолькоЦифры(Знач НомерТелефона) Экспорт
	
	Результат = "";
	ДлинаСтроки = СтрДлина(НомерТелефона);
	ДопустимыеСимволы = "1234567890";
	Для Сч = 1 по ДлинаСтроки Цикл
		
		Символ = Сред(НомерТелефона, Сч, 1);
		Если СтрНайти(ДопустимыеСимволы, Символ) > 0 Тогда
			Результат = Результат + Символ;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ЭтоМобильныйНомерТелефона(Знач НомерТелефона) Экспорт
	
	НомерТелефона = ОставитьТолькоЦифры(НомерТелефона);
	
	Если Лев(НомерТелефона, 1) = "7"
		Или СтрДлина(НомерТелефона) = 11 И Лев(НомерТелефона, 1) = "8" Тогда
		НомерТелефона = Сред(НомерТелефона, 2);
	КонецЕсли;
	
	Возврат ЭтоНомер10Символов(НомерТелефона);
	
КонецФункции

//++ Ключеров 110521

// Возвращает строку с номером телефона, приведенную к стандартизированному виду
// 
// Параметры:
// НомерТелефона - строка, Номер телефона котрый нужно привести к единому виду
Функция МобильныйНомерТелефонаСтандартныйВид(Знач НомерТелефона) Экспорт
	
	НомерТелефона = ОставитьТолькоЦифры(НомерТелефона);
	
	Если Лев(НомерТелефона, 1) = "7" Тогда
		НомерТелефона = "8" + Сред(НомерТелефона, 2);
	КонецЕсли;
	
	Если ЭтоНомер10Символов(НомерТелефона) Тогда
		НомерТелефона = "8" + НомерТелефона;
	КонецЕсли;
	
	Возврат НомерТелефона;
	
КонецФункции //МобильныйНомерТелефонаСтандартныйВид() 

// Проверяет номер на длину в 10 символов и правильное начало с 9 для этой длины
Функция ЭтоНомер10Символов(Знач НомерТелефона)
	
	Если СтрДлина(НомерТелефона) = 10 И Лев(НомерТелефона, 1) = "9" Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции //ЭтоНомер10Символов()
//-- Ключеров 110521
