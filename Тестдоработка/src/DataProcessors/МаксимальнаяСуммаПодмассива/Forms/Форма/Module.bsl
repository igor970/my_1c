// На входе массив чисел, например: А = [1, -2, 5, 2, -9, 6].
// Задача: найти непрерывный подмассив в переданном массиве, сумма элементов в котором максимальна.
// Для массива А максимальной суммой будет 7, которую дадут элементы: [5, 2].
//
// Самое главное условие - задачу требуется решить за 1 цикл по переданному массиву.
// Для проверки нужно написать тело функции МаксимальнаяСумма().

// !!! Пояснительная записка:
// В тестах полностью отрицательные массивы проверяются на 0, при этом в задании такого условия не увидел, возможно, не так понял.
// А раз нет такого условия значит такую ситуацию нужно предусмотреть. 
// Поскольку любая сумма отрицательных чисел даст Меньшее значение по отношению к любому из операндов, то возвращаем наибольшее из отрицательных как сумму,
// чем-то похоже будет на сумму для этого массива [100, -9, 2, -3, 5]
// Так же в тесты добавил правку для полностью отрицательных массивов, выделил закладками

// 1. Проверка на пустой массив, вычислять пустоту не имеет смысла
// 2. Определение переменных для процедур обработки смешанных и полностью отрицательных массивов
// 3. Обработка данных
// 4. Вывод максимиального отрезка по заданию
&НаКлиенте
Функция МаксимальнаяСумма(Массив)
	
	// 1. Проверка на пустой массив, вычислять пустоту не имеет смысла, сумма всегда 0
	Если Массив.Количество() = 0 Тогда
		Возврат 0;
	КонецЕсли;
	
	// 2. Определение переменных для процедур обработки смешанных и полностью отрицательных массивов
	// Переменные для смешанного массива
	МаксСумма    = 0;
	ТекСумма     = 0;
	ПодМассив    = Новый Массив;
	ТекПодМассив = Новый Массив;
	
	// Переменные для отрицательного массива
	ЭтоМассивОтрицательныхЧисел = Истина;
	МаксСуммаОтрицательного     = Неопределено;
	
	// 3. Обработка данных
	Для Каждого Элемент Из Массив Цикл
		
		НайтиПодмассивИзСмешанногоМассива(Элемент, МаксСумма, ТекСумма, ТекПодМассив, ПодМассив);
		НайтиПодмассивИзОтрицательногоМассива(Элемент, ЭтоМассивОтрицательныхЧисел, МаксСуммаОтрицательного);
		
	КонецЦикла;

	Если ЭтоМассивОтрицательныхЧисел Тогда
		
		МаксСумма = МаксСуммаОтрицательного;
		ПодМассив = Новый Массив();
		ПодМассив.Добавить(МаксСумма);
		
	КонецЕсли;
	
	// 4.  Вывод максимиального отрезка по заданию
	Сообщение = Новый СообщениеПользователю();
	Сообщение.Текст = СтрШаблон(НСтр("ru = 'Для массива %1 максимальный отрезок %2'"),
								"[" + СтрСоединить(Массив, ",") + "]",
								"[" + СтрСоединить(ПодМассив, ",") + "]");
	Сообщение.Сообщить();

	Возврат МаксСумма;	
	
КонецФункции

#Область Тесты

&НаКлиенте
Процедура Проверить(Команда)
	
	Тесты = СписокТестов();
	
	Для Каждого Тест Из Тесты Цикл
		Если МаксимальнаяСумма(Тест.Массив) <> Тест.Значение Тогда
			ТекстОшибки = НСтр("ru='Максимальная сумма [%1](%2) <> %3'");
			ВызватьИсключение СтрШаблон(ТекстОшибки, СтрСоединить(Тест.Массив, ","), МаксимальнаяСумма(Тест.Массив), Тест.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Сообщить(НСтр("ru='Все тесты успешно прошли!'"));
	
КонецПроцедуры

&НаКлиенте
Функция СписокТестов()
	
	Результат = Новый Массив;
	
	Тест = Новый Структура;
	Тест.Вставить("Массив", ЗначенияМассиваВЧисла(СтрРазделить("1, 2, 3", ",")));
	Тест.Вставить("Значение", 6); // Максимальный отрезок - [1, 2, 3]
	
	Результат.Добавить(Тест);
	
	
	Тест = Новый Структура;
	Тест.Вставить("Массив", ЗначенияМассиваВЧисла(СтрРазделить("2, -1, 2, 3, -9", ",")));
	Тест.Вставить("Значение", 6); // Максимальный отрезок - [2, -1, 2, 3]
	
	Результат.Добавить(Тест);
	
	
	Тест = Новый Структура;
	Тест.Вставить("Массив", ЗначенияМассиваВЧисла(СтрРазделить("-1, 2, 3, -9", ",")));
	Тест.Вставить("Значение", 5); // Максимальный отрезок - [2, 3]
	
	Результат.Добавить(Тест);
	
	
	Тест = Новый Структура;
	Тест.Вставить("Массив", ЗначенияМассиваВЧисла(СтрРазделить("-1, 2, 3, -9, 11", ",")));
	Тест.Вставить("Значение", 11); // Максимальный отрезок - [11]
	
	Результат.Добавить(Тест);
	
	
	Тест = Новый Структура;
	Тест.Вставить("Массив", ЗначенияМассиваВЧисла(СтрРазделить("-2, -1, 1, 2", ",")));
	Тест.Вставить("Значение", 3); // Максимальный отрезок - [1, 2]
	
	Результат.Добавить(Тест);
	
	
	Тест = Новый Структура;
	Тест.Вставить("Массив", ЗначенияМассиваВЧисла(СтрРазделить("100, -9, 2, -3, 5", ",")));
	Тест.Вставить("Значение", 100); // Максимальный отрезок - [100]
	
	Результат.Добавить(Тест);
	
	
	Тест = Новый Структура;
	Тест.Вставить("Массив", Новый Массив);
	Тест.Вставить("Значение", 0); // Максимальный отрезок - []
	
	Результат.Добавить(Тест);
	
	
	Тест = Новый Структура;
	Тест.Вставить("Массив", ЗначенияМассиваВЧисла(СтрРазделить("-1", ",")));
	//++ Ключеров 110521 Правка, если масив весь отрицательный, то максимальной суммой будет наибольший отрицательный элемент
	Тест.Вставить("Значение", -1); // Максимальный отрезок - [-1]
	// исх.
	//Тест.Вставить("Значение", 0); // Максимальный отрезок - []
	//-- Ключеров 110521
	
	Результат.Добавить(Тест);
	
	
	Тест = Новый Структура;
	Тест.Вставить("Массив", ЗначенияМассиваВЧисла(СтрРазделить("-1, -2", ",")));
	//++ Ключеров 110521 Правка, если масив весь отрицательный, то максимальной суммой будет наибольший отрицательный элемент
	Тест.Вставить("Значение", -1); // Максимальный отрезок - [-1]
	// исх.
	//Тест.Вставить("Значение", 0); // Максимальный отрезок - []
	//-- Ключеров 110521

	Результат.Добавить(Тест);
	
	
	Тест = Новый Структура;
	Тест.Вставить("Массив", ЗначенияМассиваВЧисла(СтрРазделить("2, -8, 5, -1, 2, -3, 2", ",")));
	Тест.Вставить("Значение", 6); // Максимальный отрезок - [5, -1, 2]
	
	Результат.Добавить(Тест);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция ЗначенияМассиваВЧисла(Массив)
	
	Для Индекс = 0 По Массив.ВГраница() Цикл
		Массив[Индекс] = Число(СокрЛП(Массив[Индекс]));
	КонецЦикла;
	
	Возврат Массив;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обработка данных для массива из одних отрицательных чисел
&НаКлиенте
Процедура НайтиПодмассивИзОтрицательногоМассива(Знач Элемент, ЭтоМассивОтрицательныхЧисел, МаксСуммаОтрицательного)
	
	// Флаг проверки массива все элементы отрицательные
	Если Элемент >= 0 Тогда
		ЭтоМассивОтрицательныхЧисел = Ложь;
	КонецЕсли;
	
	// Для первого прохода инициализируем МаксСуммаОтрицательного, далее в неё будет сохранено значение наибольшее из отрицательных
	Если МаксСуммаОтрицательного = Неопределено Тогда
		МаксСуммаОтрицательного = Элемент;
 	КонецЕсли;
	
	// Для массива отрицательных чисел максимально суммой всегда будет наибольший из элементов
    МаксСуммаОтрицательного = Макс(МаксСуммаОтрицательного, Элемент);

КонецПроцедуры

// Обработка данных для смещанного массива
&НаКлиенте
Процедура НайтиПодмассивИзСмешанногоМассива(Знач Элемент, МаксСумма, ТекСумма, ТекПодМассив, ПодМассив)

	// Для получение подмассива используем временный массив для запоминания текущего отрезка
	// Если тек сумма 0, значит у нас новое начало отрезка, массив создаем заново
	Если ТекСумма = 0 Тогда
		ТекПодМассив = Новый Массив;	
	КонецЕсли;
	
	// Увеличиваем текущую сумму и добавлеям элемент в текущий массив
	ТекСумма = ТекСумма + Элемент;
	ТекПодМассив.Добавить(Элемент);
	
	// Если текущая сумма больше максимальной, значит у нас новый лидирующий отрезок
	Если ТекСумма > МаксСумма Тогда
		// Запоминмаем сумму для теста
		МаксСумма = ТекСумма;
		// Копируем массив для отображения цепочки пользователю
		ПодМассив = Новый Массив(Новый ФиксированныйМассив(ТекПодМассив));
	КонецЕсли;
	
	// Если ушли в минус, значит мы уже считаем не то т.к. в смешанном массиве всегда большим значением будет положительная сумма
	Если ТекСумма < 0 Тогда
		ТекСумма = 0;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
